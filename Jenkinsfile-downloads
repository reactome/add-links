// This Jenkinsfile is used by Jenkins to run the AddLinks-Downloads step of Reactome's release.
// It requires that the ConfirmReleaseConfigs step has been run successfully before it can be run.

import org.reactome.release.jenkins.utilities.Utilities

// Shared library maintained at 'release-jenkins-utils' repository.
def utils = new Utilities()
pipeline{
	agent any

	stages{
		// This stage checks that an upstream project, ConfirmReleaseConfigs, was run successfully for its last build.
		stage('Check ConfirmReleaseConfigs build succeeded'){
			steps{
				script{
					utils.checkUpstreamBuildsSucceeded("ConfirmReleaseConfigs")
				}
			}
		}
		// The FlyBase URL changes every few months, so this step asks the user to check the FTP repository where the download file is and report the 'yyyy_MM' part of the file name.
        	stage('User Input: Get FlyBase date in file URL and update it in basic-file-retrievers.xml'){
			steps{
				script{
					def userInputFlyBase = input(
					id: 'userInput', message: "Please submit the YYYY_MM value in in the filename of the file called \'fbgn_NAseq_Uniprot_fb_YYYY_MM.tsv.gz\'. It can be found at \'ftp://ftp.flybase.net/releases/current/precomputed_files/genes\'.",
					parameters: [
						[$class: 'TextParameterDefinition', description: "It should be in the format \'YYYY_MM\' (eg. \'2020_05\')", name: 'response']
					])

					// Checks that the submitted URL contains the string in $baseReleaseNewsURL
					if (userInputFlyBase.length() == 7 && userInputFlyBase.substring(4,5).equals("_")) {
						echo("Valid YYYY_MM value submitted, updating FlyBase file URL in basic-file-retrievers.xml")
						def fileRetrievalsConfig = "src/main/resources/basic-file-retrievers.xml"
						sh "git checkout ${fileRetrievalsConfig}"
						sh "sed -i -e 's/YYYY_MM/${userInputFlyBase}/' ${fileRetrievalsConfig}"
					} else {
						error("Invalid value submitted. Please submit a value in the format \'YYYY_MM\', such as \'2020_05\'.")
					}
				}
		    	}
		}
		// This stage builds the jar file using maven.
		stage('Setup: Build jar file'){
			steps{
				script{
					sh "mvn clean package -DskipTests"
				}
			}
		}
		// This stage executes the AddLinks file download step. It will download all files needed to run the AddLinks insertion step, 
		//storing them at /tmp/addlinks-downloaded-files.
		stage('Main: Download AddLinks files'){
			steps{
				script{
					withCredentials([file(credentialsId: 'Config', variable: 'ConfigFile')]){
						updatePersonIdInProperties("${ConfigFile}")
					    
						sh "cp $ConfigFile src/main/resources/auth.properties"
						sh "ln -sf src/main/resources resources"
						sh "rm -f src/main/resources/db.properties"
						sh """java -cp "\$(pwd)/resources" -Dconfig.location=\$(pwd)/resources/addlinks.properties -Dlog4j.configurationFile=\$(pwd)/resources/log4j2.xml -Xmx${env.JAVA_MEM_MAX}m -jar target/AddLinks-*-SHADED.jar file://\$(pwd)/resources/file-download-context.xml"""
						sh "rm -f src/main/resources/auth.properties"
					}
				}
			}
		}
		// Sends email notification to developers list regarding completion of AddLinks file downloads.
		stage('Post: Email regarding completion of file downloads'){
			steps{
				script{
					def releaseVersion = utils.getReleaseVersion()
					def emailSubject = "AddLinks-Download has completed"
					def emailBody = "Hello,\n\nThis is an automated message from Jenkins regarding an update for v${releaseVersion}. The file downloads for AddLinks has completed, and needs to be verified before it will be archived. \n\nThanks!"
					utils.sendEmail("${emailSubject}", "${emailBody}")
				}
			}
		}
		// Program is stopped and waits for confirmation that the dry run was successful.
		stage('User Input: Confirm successful AddLinks file downloads') {
			steps {
		        	script {
		            		def userInput = input(
						id: 'userInput', message: "Please proceed once you have reviewed the AddLinks file downloads.",
						parameters: [
							[$class: 'BooleanParameterDefinition', defaultValue: true, name: 'response']
						])
		       		}
		    	}
		}
		// Archive everything on S3 and then delete on the server.
		stage('Post: Archive Outputs'){
			steps{
				script{
				    def releaseVersion = utils.getReleaseVersion()
					def downloadsArchive = "addlinks-downloads-v${releaseVersion}.tar"
					def addlinksFolder = "addlinks-downloaded-files/"
					
					def dataFiles = ["/tmp/$downloadsArchive"]
					def logFiles = ["reports/*"]
					def foldersToDelete = ["reports", "/tmp/$addlinksFolder"]
					
					sh "cd /tmp/; tar -zcvf $downloadsArchive $addlinksFolder"
					utils.cleanUpAndArchiveBuildFiles("add_links/downloads", dataFiles, logFiles, foldersToDelete)
				}
			}
		}
	}
}

// This is just a localized method for updating the personId method that exists in the addlinks.properties file using grep/cut.
// TODO: Add to shared groovy library
def updatePersonIdInProperties(configFile) {
    	def personId = sh (
		script: "grep personId $configFile | cut -d = -f2",
		returnStdout: true
	).trim()
	sh "git checkout src/main/resources/addlinks.properties"
    // Splice in the person dbId from the config file	
    sh "sed -i -e 's/executeAsPersonID=/executeAsPersonID=${personId}/g' src/main/resources/addlinks.properties"
}
